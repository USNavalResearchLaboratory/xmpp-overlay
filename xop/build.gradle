import org.apache.tools.ant.taskdefs.condition.Os

def includeGCS = System.getProperty('include.gcs', 'no')

// For building kotlin
buildscript {
    ext.kotlin_version = '1.3.30'
    ext.coroutines_version = '1.2.1'
    ext.jni_dir = 'x86_64'
    if (Os.isFamily(Os.FAMILY_MAC)) {
        println "*** Mac "
        ext.jni_dir = 'darwin'
    }

    repositories {
        mavenCentral()
        maven { url "https://kotlin.bintray.com/kotlinx" }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'application'
apply plugin: 'kotlin'
apply plugin: 'java'

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    compile 'com.google.guava:guava:26.0-jre'
    compile fileTree(include: ['*.jar'], dir: 'libs')
    if( includeGCS.equals('yes')) {
        compile fileTree(include: ['*.jar'], dir: 'libs-gcs')
    } else {
        compileOnly fileTree(include: ['*.jar'], dir: 'libs-gcs')
    }
    compile fileTree(include: ['*.jar'], dir: 'libs-protosd')

    // https://mvnrepository.com/artifact/javax.xml.bind/jaxb-api
    compile 'javax.xml.bind:jaxb-api:2.3.1'
    compile 'javax.activation:javax.activation-api:1.2.0'

    compile 'org.json:json:20180813'
    compile 'org.igniterealtime:tinder:1.3.0'
    compile 'dom4j:dom4j:1.6.1'
    compile 'org.slf4j:slf4j-api:1.5.11'
    compile 'org.slf4j:slf4j-simple:1.5.11'
    //noinspection DifferentStdlibGradleVersion
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    compile "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    testImplementation fileTree(include: ['*.jar'], dir: 'libs-test')
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.1'
    testRuntime(
            "org.junit.platform:junit-platform-launcher:1.3.1",
            "org.junit.jupiter:junit-jupiter-engine:5.3.1"
    )
    testCompile(
            "org.igniterealtime.smack:smack-java7:4.3.1",
            "org.igniterealtime.smack:smack-tcp:4.3.1",
            "org.igniterealtime.smack:smack-extensions:4.3.1"
    )
}

applicationDefaultJvmArgs = ["-Djava.library.path=./jniLibs/x86_64"]

mainClassName = 'edu.drexel.xop.Run'
def implementationVersion = '1.0'
applicationName = 'xop'

jar {
    manifest {
        attributes 'Implementation-Title': 'XOP',
                    'Implementation-Version': "${implementationVersion}",
                    'Main-Class': "${mainClassName}"
    }
}

static def getTimestamp() {
    def date = new Date()
    return date.format('yyyyMMdd.HHmmss')
}

task createTarBall (type:Tar){
    dependsOn 'deployXOP'

    from ('../') {
        if( !includeGCS.equals('yes') ) {
            include 'dist/**'
        } else {
            include 'dist-gcs/**'
        }
    }

    destinationDir = file('../zips')
        baseName = applicationName
        appendix = 'dist'
        version = getTimestamp()
        extension 'tgz'
        compression = Compression.GZIP
}

task deployXOP (type:Copy) {
    dependsOn 'fatJar'

    println("includeGCS: ${includeGCS}")

    from('bin') {
        include '*.sh'
        include '*.bat'
    }

    from('build/libs'){
        include 'xop-all.jar'
    }

    from('..') {
        include 'README.md'
        include 'RUNNING.md'
        include 'CHANGELOG.txt'
        include 'REVISION.txt'
    }

    from('.'){
        include 'jniLibs/'
        include 'config/'
    }

    if( !includeGCS.equals('yes') ) {
        into '../dist'
    } else {
        into '../dist-gcs'
    }
}

task fatJar(type: Jar) {
    dependsOn build
    manifest {
        attributes 'Implementation-Title': 'XOP',
                'Implementation-Version': '1.0',
                'Main-Class': "${mainClassName}"
    }
    baseName = project.name + '-all'
    from {
        if( includeGCS.equals('yes')) {
            configurations.compile.collect {
                it.isDirectory() ? it : zipTree(it)
            }
        } else {
            configurations.compile.collect {
                if( !it.getPath().contains('libs-gcs') ) {
                    it.isDirectory() ? it : zipTree(it)
                } else {
                    it
                }
            }
        }
    }
    with jar
}

clean.doFirst {
    if( !includeGCS.equals('yes') ) {
        println "Removing ../dist"
        delete '../dist'
    } else {
        println "Removing ../dist-gcs"
        delete '../dist-gcs'
    }
}

import java.text.DateFormat
import java.text.SimpleDateFormat

static def getDateTime() {
    DateFormat df = new SimpleDateFormat("yyyyMMddHHmm")
    return df.format(new Date())
}

distZip {
    def dt = getDateTime()
    def archiveBaseName = "$baseName"
    archiveName archiveBaseName+".zip"
    into(archiveBaseName) {
        from '.'
        include 'config/*'
        include 'jniLibs/**/*'
        include 'bin/*'
    }

    into(archiveBaseName) {
        from '..'
        include 'REVISION.txt'
    }
}

test {
    println("using library path: $jni_dir")
    systemProperties = [
            'junit.jupiter.execution.parallel.enabled': 'false',
            "java.library.path": "jniLibs/$jni_dir"
    ]
    useJUnitPlatform {
        // includeTags 'fast', 'smoke & feature-a'
        // excludeTags 'slow', 'ci'
        includeEngines 'junit-jupiter'
        // excludeEngines 'junit-vintage'
    }
    // 2019-02-19: This is a temporary hack: does not pass running from the commandline
    exclude 'mil/navy/nrl/xop/client/ClientConnectionTest.class'
}
